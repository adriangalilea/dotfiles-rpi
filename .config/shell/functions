#!/usr/bin/env bash

# --- history filtering ---
zshaddhistory() {
    emulate -L zsh
    [[ ${1%%$'\n'} != ${~HISTORY_IGNORE} ]]
}

# --- directories ---

# Create directory and cd into it
mc() {
  if [ $# -ne 1 ]; then
    echo "usage: mc <dir-name>"
    return 137
  fi
  local dir_name="$1"
  mkdir -p "$dir_name" && cd "$dir_name"
}

# Display or change the directory stack
d() {
  if [[ -n $1 ]]; then
    dirs "$@"
  else
    dirs -v | head -n 10
  fi
}
compdef _dirs d

# Change to the previous directory only on non mac systems
# if [[ "$(uname -s)" != "Darwin" ]]; then
#   -() {
#     cd -
#   }
# fi

# --- file listing ---

files() {
    if [[ ! -x "$(command -v eza)" ]]; then
        echo -e "\033[1;33mMagic Dashboard: \`eza\` not installed.\033[0m" && return 1
    fi
    echo

    zoxide add "$PWD"
    
    eza --icons --color=always --git -d "$PWD"
    
    echo
    
    local eza_output
    local max_files_lines=${MAGIC_DASHBOARD_FILES_LINES:-6}
    local ignore_pattern=".DS_Store|Icon?|Thumbs.db|desktop.ini"
    eza_output=$(
        eza --width="$COLUMNS" --grid --color=always --icons \
            --ignore-glob="$ignore_pattern" \
            --sort=name --group-directories-first --no-quotes \
            --git --long --no-user --no-permissions --no-filesize --no-time
    )
    if [[ $(echo "$eza_output" | wc -l) -gt $max_files_lines ]]; then
        local shortened
        shortened="$(echo "$eza_output" | head -n"$max_files_lines")"
        printf "%s \033[1;36m…\033[0m\n" "$shortened"
    elif [[ -n "$eza_output" ]]; then
        echo "$eza_output"
    fi
    echo
}

# --- Ctrl+S zsh sourcing ---

source_zshrc() {
  source ~/.zshrc
}

# Disable terminal flow control (Ctrl+S/Ctrl+Q) to free up Ctrl+S
if [[ $- == *i* ]] && [[ -t 0 ]]; then
  stty -ixon 2>/dev/null || true
fi

zle -N source_zshrc
bindkey "^s" source_zshrc

# --- Ctrl+G lazygit ---

run_lazygit() {
  lazygit
}
zle -N run_lazygit
bindkey "^g" run_lazygit

# --- Ctrl+H helix ---

run_helix() {
  h
}
zle -N run_helix
bindkey "^h" run_helix


# --- Yazi ---

function yy() {
	local tmp="$(mktemp -t "yazi-cwd.XXXXXX")"
	yazi "$@" --cwd-file="$tmp"
	if cwd="$(cat -- "$tmp")" && [ -n "$cwd" ] && [ "$cwd" != "$PWD" ]; then
		cd -- "$cwd"
	fi
	rm -f -- "$tmp"
}

zle -N yy
bindkey "^y" yy


# --- add to .gitignore ---

add_to_dotfiles() {
  local path="${1:-.}"
  local gitignore_path="$HOME/.gitignore"

  echo

  # Colors
  local GREEN='\033[0;32m'
  local RED='\033[0;31m'
  local YELLOW='\033[0;33m'
  local GRAY='\033[0;90m'
  local NC='\033[0m' # No Color

  # Help function
  if [[ "$path" == "-h" || "$path" == "--help" ]]; then
    echo -e "${GRAY}Usage:${NC} add [path]"
    echo
    echo "Add a file or directory to .gitignore"
    echo "If no path is provided, the current directory will be added"
    echo
    echo -e "${GRAY}⚠ This is only useful if you have ~ as a git repo.${NC}"
    return 0
  fi

  # Convert to relative path from home directory
  [[ "$path" = /* ]] && path="${path#$HOME/}" || path="${PWD#$HOME/}/${path#./}"
  path="${path#/}"
  path="${path%/.}"

  # Read .gitignore content
  local gitignore_content
  gitignore_content=$(<"$gitignore_path")

  # Check if the path is already in .gitignore
  if [[ $gitignore_content == *"!$path"* ]]; then
    echo -e "${YELLOW}⚠ Path $path is already in .gitignore${NC}"
    return 0
  fi

  local added=false

  # If it's a directory, add it and its contents
  if [[ -d "$HOME/$path" ]]; then
    echo "!$path/" >> "$gitignore_path"
    echo "!$path/**" >> "$gitignore_path"
    echo -e ".gitignore  <--  ${GREEN}+ !$path/${NC}"
    echo -e ".gitignore  <--  ${GREEN}+ !$path/**${NC}"
    added=true
  else
    # It's a file, add it directly
    echo "!$path" >> "$gitignore_path"
    echo -e ".gitignore  <--  ${GREEN}+ !$path${NC}"
    added=true
  fi

  # If it's a .config subdirectory, ensure the parent directory is also included
  if [[ "$path" == .config/* ]]; then
    local config_parent="${path%/*}"
    if [[ $gitignore_content != *"!$config_parent/"* ]]; then
      echo "!$config_parent/" >> "$gitignore_path"
      echo -e ".gitignore  <--  ${GREEN}+ !$config_parent/${NC}"
    fi
  fi

  if ! $added; then
    echo -e "${RED}✘ No changes were made to .gitignore${NC}"
  fi
}

alias add="add_to_dotfiles"

# --- toptal gitignore ---
# example usage `gi mac,go >> .gitignore`
function gi() {
  local choice
  while true; do
    choice=$(gum choose --header "What do you want to do?" "Create .gitignore" "Show output" "Quit")
    case $choice in
      "Create .gitignore")
        local templates=$(echo $@ | tr ' ' ',')
        local content=$(curl -sL "https://www.toptal.com/developers/gitignore/api/$templates")
        echo "$content" > .gitignore
        echo ".gitignore created"
        break
        ;;
      "Show output")
        local templates=$(echo $@ | tr ' ' ',')
        local content=$(curl -sL "https://www.toptal.com/developers/gitignore/api/$templates")
        echo "$content" | gum pager
        ;;
      "Quit")
        break
        ;;
    esac
  done
}

_gitignoreio_get_command_list() {
  curl -sL https://www.toptal.com/developers/gitignore/api/list | tr "," "\n"
}

_gitignoreio() {
  local words
  words=(${(z)LBUFFER})
  if [[ ${#words} -gt 1 ]]; then
    _values -s , 'templates' $(_gitignoreio_get_command_list)
  else
    compadd -S '' $(_gitignoreio_get_command_list)
  fi
}

compdef _gitignoreio gi

# alternatively this exists https://www.reddit.com/r/rust/comments/1657lyz/built_my_first_rust_app_cli_tool_to_generate/
# https://github.com/reemus-dev/gitnr
# but I find gi easier to use

# Navigation history array
typeset -g -a _nav_history
typeset -g _nav_index

# Initialize navigation history
_nav_init() {
    _nav_history=("$PWD")
    _nav_index=1
}

# Navigation function
nav() {
    case "$1" in
        back)
            if (( _nav_index > 1 )); then
                (( _nav_index-- ))
                cd "${_nav_history[$_nav_index]}"
            fi
            ;;
        forward)
            if (( _nav_index < ${#_nav_history[@]} )); then
                (( _nav_index++ ))
                cd "${_nav_history[$_nav_index]}"
            fi
            ;;
        up)
            cd ..
            _nav_history[_nav_index]="$PWD"
            ;;
        down)
            zi
            _nav_history[_nav_index]="$PWD"
            ;;
        *)
            if [[ -d "$1" ]]; then
                cd "$1"
                if [[ "${_nav_history[$_nav_index]}" != "$PWD" ]]; then
                    _nav_history=(${_nav_history[1,$_nav_index]} "$PWD")
                    _nav_index=${#_nav_history[@]}
                fi
            else
                echo "Usage: nav [back|forward|up|down|directory]" >&2
            fi
            ;;
    esac
}

# Initialize navigation history
_nav_init

# Key bindings
bindkey -s '^[[1;2A' 'nav up\n'      # shift + up
bindkey -s '^[[1;2B' 'nav down\n'    # shift + down
bindkey -s '^[[1;2C' 'nav forward\n' # shift + right
bindkey -s '^[[1;2D' 'nav back\n'    # shift + left


# Add the current directory to history when changing directories
chpwd() {
    if [[ "${_nav_history[$_nav_index]}" != "$PWD" ]]; then
        _nav_history=(${_nav_history[1,$_nav_index]} "$PWD")
        _nav_index=${#_nav_history[@]}
    fi
}


# --- dotfiles setup ---
install_dotfiles() {
  sudo apt update -y
  sudo apt install zsh -y
  chsh -s /bin/zsh
  cd ~/.config/setup
  chmod +x main.zsh
  sudo ./main.zsh rpi_aarch64.cue
}

# --- go home ---
home() {
  cd
}
alias ~="home"

# --- check anthropic rate limits ---
check_limits() {
  # Ensure the API key is set
  if [ -z "$ANTHROPIC_API_KEY" ]; then
    echo "Error: ANTHROPIC_API_KEY environment variable is not set."
    return 1
  fi

  # Make the API request and save the headers in a variable
  response=$(curl -s -D - -o /dev/null -H "Authorization: Bearer $ANTHROPIC_API_KEY" \
     --header "x-api-key: $ANTHROPIC_API_KEY" \
     --header "anthropic-version: 2023-06-01" \
     --header "content-type: application/json" \
     --data '{
       "model": "claude-3-5-sonnet-20240620",
       "max_tokens": 1,
       "messages": [
         {"role": "user", "content": "Hello"}
       ]
     }' \
     https://api.anthropic.com/v1/messages)

  # Extract and print the relevant headers
  echo "Request Rate Limit: $(echo "$response" | grep -i 'anthropic-ratelimit-requests-limit:' | awk '{print $2}')"
  echo "Requests Remaining: $(echo "$response" | grep -i 'anthropic-ratelimit-requests-remaining:' | awk '{print $2}')"
  echo "Requests Reset Time: $(echo "$response" | grep -i 'anthropic-ratelimit-requests-reset:' | awk '{print $2}')"
  echo "Token Rate Limit: $(echo "$response" | grep -i 'anthropic-ratelimit-tokens-limit:' | awk '{print $2}')"
  echo "Tokens Remaining: $(echo "$response" | grep -i 'anthropic-ratelimit-tokens-remaining:' | awk '{print $2}')"
  echo "Tokens Reset Time: $(echo "$response" | grep -i 'anthropic-ratelimit-tokens-reset:' | awk '{print $2}')"
  echo "Retry After: $(echo "$response" | grep -i 'retry-after:' | awk '{print $2}')"
}

